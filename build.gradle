// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.0-alpha07'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0-SNAPSHOT"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.22.0"
    id "org.jlleitschuh.gradle.ktlint" version "8.2.0"
    id 'io.gitlab.arturbosch.detekt' version '1.0.1'
}

// https://github.com/vanniktech/gradle-android-junit-jacoco-plugin
apply plugin: "com.vanniktech.android.junit.jacoco"

junitJacoco {
    jacocoVersion = '0.8.4'
    excludes = [
            '**/*Module.*',
            '**/BuildConfig.*',
            '**/di/*',
            '**/*_MembersInjector*.*',
            '**/R.class',
            '**/R$*.class'
    ]

    includeInstrumentationCoverageInMergedReport = true
}

allprojects {
    repositories {
        google()
        jcenter()

    }
}

subprojects {
    // https://github.com/jlleitschuh/ktlint-gradle
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "org.jlleitschuh.gradle.ktlint-idea"

    ktlint {
        debug = true
    }

    // https://github.com/arturbosch/detekt
    apply plugin: 'io.gitlab.arturbosch.detekt'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
