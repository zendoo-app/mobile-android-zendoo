apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 26
        //noinspection OldTargetApi
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        debug {
            debuggable true
            manifestPlaceholders = [enableCrashReporting: "false"]
            testCoverageEnabled true
        }
        release {
            manifestPlaceholders = [enableCrashReporting: "true"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        preDexLibraries true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'

        animationsDisabled true

        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
        enable 'AppLinksAutoVerifyError',
                'AppLinksAutoVerifyWarning',
                'BackButton',
                'ConvertToWebp',
                'DalvikOverride',
                'DuplicateStrings',
                'EasterEgg',
                'FieldGetter',
                'GoogleAppIndexingApiWarning',
                'IconExpectedSize',
                'ImplicitSamInstance',
                'KotlinPropertyAccess',
                'LambdaLast',
                'LockedOrientationActivity',
                'LogConditional',
                'MangledCRLF',
                'MinSdkTooLow',
                'MissingRegistered',
                'NegativeMargin',
                'NewerVersionAvailable',
                'NoHardKeywords',
                'NonResizeableActivity',
                'PermissionImpliesUnsupportedChromeOsHardware',
                'Registered',
                'RequiredSize',
                'SelectableText',
                'SourceLockedOrientationActivity',
                'StopShip',
                'SyntheticAccessor',
                'TypographyQuotes',
                'UnknownNullness',
                'UnpackedNativeCode',
                'UnsupportedChromeOsCameraSystemFeature',
                'UnsupportedChromeOsHardware',
                'UnusedIds',
                'ValidActionsXml',
                'WrongThreadInterprocedural'
    }
}

dependencies {
    implementation project(':features:dashboard')
    implementation project(':libraries:di-components')
    implementation project(':libraries:ui-components')

    androidTestImplementation 'io.mockk:mockk-android:1.9.3'
    androidTestImplementation 'androidx.arch.core:core-testing:2.0.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2-alpha02'
    androidTestImplementation 'androidx.test.ext:truth:1.2.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'

    androidTestUtil 'androidx.test:orchestrator:1.2.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core-ktx:1.2.0-alpha03'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha03'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.0-alpha01'
    implementation 'androidx.navigation:navigation-fragment:2.2.0-alpha01'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.0-alpha01'
    implementation 'androidx.navigation:navigation-ui:2.2.0-alpha01'
    implementation 'com.google.android.material:material:1.1.0-alpha09'
    implementation 'com.google.firebase:firebase-core:17.2.0'
    implementation 'com.google.firebase:firebase-perf:19.0.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    kapt 'com.google.dagger:dagger-android-processor:2.24'
    kapt 'com.google.dagger:dagger-compiler:2.24'

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation 'androidx.arch.core:core-testing:2.0.1'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'androidx.test.ext:truth:1.2.0'
    testImplementation 'io.mockk:mockk:1.9.3'
    testImplementation 'junit:junit:4.13-beta-3'
    testImplementation 'org.mockito:mockito-android:3.0.0'
    testImplementation 'org.mockito:mockito-core:3.0.0'
    testImplementation 'org.mockito:mockito-inline:3.0.0'
}

apply plugin: 'com.google.gms.google-services'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "500"
    }
}
